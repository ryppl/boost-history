# vi: syntax=python:et:ts=4
Import("env")
env.Append(
    CPPPATH = "#/",
    CPPDEFINES = ["BOOST_ALL_NO_LIB=1"],
    )

sconscripts = Glob("*/build/SConscript")
env.AppendUnique(CPPDEFINES = ["${LINK_DYNAMIC and 'BOOST_' + BOOST_LIB + '_DYN_LINK=1' or []}"])
for variant in env["variant"]:
    env.SetProperty(profile = False)
    if variant == "release":
        env.SetProperty(optimize = "speed", debug = False)
    elif variant == "debug":
        env.SetProperty(optimize = "no", debug = True)
    elif variant == "profile":
        env.SetProperty(optimize = "speed", profile = True, debug = True)
    for linking in env["link"]:
        if linking == "dynamic":
            env["LINK_DYNAMIC"] = True
        else:
            env["LINK_DYNAMIC"] = False
        for threading in env["threading"]:
            env.SetProperty(threading = threading)
            for sconscript in sconscripts:
                lib = str(sconscript.get_dir().up())
                target_dir = "#/bin.SCons/" + lib + "/" + variant + "/" + linking + "/threading-" + threading
                VariantDir(src_dir = lib + "/src", variant_dir = target_dir, duplicate = False)
                VariantDir(src_dir = lib + "/build", variant_dir = lib + "/src", duplicate = False)
                SConscript(target_dir + "/SConscript", exports = { "env" : env.Clone(BOOST_LIB = lib.upper()) })
